plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.0.1'
}

group 'org.gemseeker'
version '1.0.2'

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.gemseeker.sms'
    mainClass = 'org.gemseeker.sms.AppMain'
}

javafx {
    version = '21.0.3'
    modules = [
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics',
            'javafx.web',
            'javafx.media',
            'javafx.swing'
    ]
}

dependencies {
    implementation("com.h2database:h2:2.2.222")
    implementation("commons-io:commons-io:2.16.1")
    implementation("org.reactivestreams:reactive-streams:1.0.4")
    implementation("io.reactivex.rxjava2:rxjava:2.2.21")
    implementation("io.reactivex.rxjava2:rxjavafx:2.2.2")
    implementation("io.github.msufred:feathericons:1.0.0"){
        exclude group: 'org.openjfx'
    }

    // map
    implementation group: 'org.jxmapviewer', name: 'jxmapviewer2', version: '2.8'

    // Apache POI and Dependencies
    implementation group: 'org.apache.poi', name: 'poi', version: '5.2.5'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.5'
    implementation group: 'org.apache.poi', name: 'poi-ooxml-lite', version: '5.2.5'
    implementation group: 'com.zaxxer', name: 'SparseBitSet', version: '1.3'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.17.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.23.1'
    implementation group: 'com.github.virtuald', name: 'curvesapi', version: '1.08'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.26.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
    implementation group: 'org.apache.xmlbeans', name: 'xmlbeans', version: '5.2.0'


    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}"){
        exclude group: 'org.openjfx'
    }
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}"){
        exclude group: 'org.openjfx'
    }
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'smsapp'
    }
    addExtraDependencies('javafx')
}

jlinkZip {
    group = 'distributions'
}

task dist {
    dependsOn clean, jlinkZip
    description "clean then jlinkZip [default]"
}

defaultTasks 'dist'

tasks.jlink.doLast {
    copy {
        from 'res'
        include 'res/**'
        into "${buildDir}/jpackage/smsapp"
    }
}